QDOC = $$[QT_INSTALL_BINS]/qdoc
QHELPGENERATOR = $$[QT_INSTALL_BINS]/qhelpgenerator

# Note: not $${QT_INSTALL_DOCS}/sailfish-qdoc-template
# because the SDK prevents installation to /usr/share/doc/
isEmpty(SAILFISH_QDOC.template):SAILFISH_QDOC.template=/usr/share/sailfish-qdoc-template
isEmpty(SAILFISH_QDOC.style):SAILFISH_QDOC.style=offline

QDOCCONF = $${SAILFISH_QDOC.template}/$${SAILFISH_QDOC.style}.qdocconf
QHELPFILE = html/$${SAILFISH_QDOC.project}.qhp
QCHFILE = $${SAILFISH_QDOC.project}.qch

ENV += \
    QT_VERSION=$${QT_MAJOR_VERSION}.$${QT_MINOR_VERSION} \
    PROJECT_CONFIG=$${_PRO_FILE_PWD_}/$${SAILFISH_QDOC.config} \
    BUILD_DIRECTORY=$${OUT_PWD} \
    TEMPLATE=$${SAILFISH_QDOC.template}

qch_docs.target = $${QCHFILE}
qch_docs.depends += FORCE
qch_docs.commands = (env $$ENV $$QDOC $$QDOCCONF) && \
                    ($${QHELPGENERATOR} $${QHELPFILE} -o $${QCHFILE})

generate_docs.target = docs
generate_docs.depends = $${QCHFILE}

docs.depends = $${QCHFILE}
docs.path = $${SAILFISH_QDOC.path}
docs.commands = $$QMAKE_INSTALL_FILE $${QCHFILE} $(INSTALL_ROOT)$${docs.path}

QMAKE_EXTRA_TARGETS += qch_docs generate_docs
INSTALLS += docs

OTHER_FILES += $${SAILFISH_QDOC.config}
