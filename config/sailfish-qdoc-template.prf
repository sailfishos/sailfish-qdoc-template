QDOC = $$[QT_INSTALL_BINS]/qdoc
QHELPGENERATOR = $$[QT_INSTALL_BINS]/qhelpgenerator

# Note: not $${QT_INSTALL_DOCS}/sailfish-qdoc-template
# because the SDK prevents installation to /usr/share/doc/
isEmpty(SAILFISH_QDOC.template):SAILFISH_QDOC.template=/usr/share/sailfish-qdoc-template
isEmpty(SAILFISH_QDOC.style):SAILFISH_QDOC.style=offline
isEmpty(SAILFISH_QDOC.base_url):SAILFISH_QDOC.base_url=https://sailfishos.org/develop/docs

# Main concern here is to ensure that $$dirname() use is not affected
# by possible trailing slash
SAILFISH_QDOC.path = $$clean_path($${SAILFISH_QDOC.path})

QDOCCONF = $${SAILFISH_QDOC.template}/$${SAILFISH_QDOC.style}.qdocconf
QHELPFILE = html/$${SAILFISH_QDOC.project}.qhp
QCHFILE = $${SAILFISH_QDOC.project}.qch

ENV += \
    QT_VERSION=$${QT_MAJOR_VERSION}.$${QT_MINOR_VERSION} \
    PROJECT_CONFIG=$${_PRO_FILE_PWD_}/$${SAILFISH_QDOC.config} \
    BUILD_DIRECTORY=$${OUT_PWD} \
    BASE_URL=$${SAILFISH_QDOC.base_url} \
    TEMPLATE=$${SAILFISH_QDOC.template}

docs.input = ..
docs.output = $${QCHFILE}
docs.commands = (env $$ENV $$QDOC $$QDOCCONF \
                     -installdir $${SAILFISH_QDOC.path} \
                     -indexdir $$dirname(SAILFISH_QDOC.path)) && \
                ($${QHELPGENERATOR} $${QHELPFILE} -o $${QCHFILE})
docs.CONFIG += no_check_exist no_default_install

docs_install.files = $${QCHFILE}
docs_install.path = $${SAILFISH_QDOC.path}
docs_install.CONFIG += no_check_exist no_build

QMAKE_EXTRA_TARGETS += docs
PRE_TARGETDEPS += docs

INSTALLS += docs_install

OTHER_FILES += $${SAILFISH_QDOC.config}
